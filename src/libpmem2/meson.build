sources = files()
compile_args = []
includes = [include_directories('.')]

if host_machine.cpu_family() == 'aarch64'
    sources += files('aarch64/init.c')
elif host_machine.cpu_family() == 'ppc64'
    sources += files('ppc64/init.c')
elif host_machine.cpu_family() == 'riscv64'
    sources += files('riscv64/init.c')
elif host_machine.cpu_family() == 'x86_64'
    sources += files(
        'x86_64/cpu.c',
        'x86_64/init.c',
        'x86_64/memcpy/memcpy_nt_avx.c',
        'x86_64/memcpy/memcpy_nt_sse2.c',
        'x86_64/memset/memset_nt_avx.c',
        'x86_64/memset/memset_nt_sse2.c',
        'x86_64/memcpy/memcpy_t_avx.c',
        'x86_64/memcpy/memcpy_t_sse2.c',
        'x86_64/memset/memset_t_avx.c',
        'x86_64/memset/memset_t_sse2.c',
    )

    test_avx512f = cc.run(
        '''
        #include <immintrin.h>
        #include <stdint.h>

        int main(void) {
            uint64_t v[8];
            __m512i zmm0 = _mm512_loadu_si512((__m512i *)&v);

            return 0;
        }
        ''',
        args: ['-mavx512f'],
        name: 'avx512f support'
    )
    if test_avx512f.compiled() and test_avx512f.returncode() == 0
        message('Running avx512f')
        sources += files(
            'x86_64/memcpy/memcpy_nt_avx512f.c',
            'x86_64/memcpy/memcpy_t_avx512f.c',
            'x86_64/memset/memset_nt_avx512f.c',
            'x86_64/memset/memset_t_avx512f.c',
        )

        compile_args += [
            '-DAVX512F_AVAILABLE=1',
            '-mavx512f',
        ]
        pmem2_use_avx512f = 1
    else
        compile_args += '-DAVX512F_AVAILABLE=0'
        pmem2_use_avx512f = 0
    endif

    test_movdir64b = cc.run(
        '''
        #include <immintrin.h>
        #include <stdint.h>

        int main(){
            uint64_t v[8], w[8];
            _movdir64b(&v, &w);
            return 0;
        }
        ''',
        args: ['-mmovdir64b'],
        name: 'movdir64b support'
    )
    if test_movdir64b.compiled() and test_movdir64b.returncode() == 0
        message('Running movdir64b')
        sources += files(
            'x86_64/memcpy/memcpy_nt_movdir64b.c',
            'x86_64/memset/memset_nt_movdir64b.c',
        )

        compile_args += [
            '-DMOVDIR64B_AVAILABLE=1',
            '-mmovdir64b',
        ]
        pmem2_use_movdir64b = 1
    else
        compile_args += '-DMOVDIR64B_AVAILABLE=0'
        pmem2_use_movdir64b = 0
    endif

    includes += include_directories('x86_64/memcpy', 'x86_64/memset')

    compile_args += '-mavx'
else
    error('pmdk does not support @0@'.format(host_machine.cpu_family()))
endif

includes += include_directories(host_machine.cpu_family())

pmem2arch_dep = declare_dependency(
    compile_args: compile_args,
    sources: sources,
    include_directories: includes
)

sources = files(
    'libpmem2.c',
    'config.c',
    'deep_flush.c',
    'errormsg.c',
    'map.c',
    'memops_generic.c',
    'mover.c',
    'persist.c',
    'pmem2_utils.c',
    'source.c',
    'vm_reservation.c'
)

if host_machine.system() == 'linux'
    sources += files(
        'auto_flush_linux.c',
        'deep_flush_linux.c',
        'extent_linux.c',
        'map_posix.c',
        'mcsafe_ops_posix.c',
        'persist_posix.c',
        'pmem2_utils_linux.c',
        'source_posix.c',
        'vm_reservation_posix.c'
    )

    if libndctl_dep.found()
        sources += files(
            'badblocks_ndctl.c',
            'numa_ndctl.c',
            'pmem2_utils_ndctl.c',
            'region_namespace_ndctl.c',
            'usc_ndctl.c'
        )
    else
        sources += files(
            'badblocks_none.c',
            'numa_none.c',
            'pmem2_utils_none.c',
            'region_namespace_none.c',
            'usc_none.c'
        )
    endif
elif host_machine.system() == 'windows'
    sources += files(
        'auto_flush_none.c',
        'badblocks_none.c',
        'deep_flush_other.c',
        'extent_none.c',
        'map_windows.c',
        'mcsafe_ops_windows.c',
        'numa_none.c',
        'persist_windows.c',
        'pmem2_utils_other.c',
        'region_namespace_none.c',
        'source_windows.c',
        'usc_windows.c',
        'vm_reservation_windows.c',
    )
endif

link_args = []

if host_machine.system() == 'linux'
    link_args += '-Wl,--version-script=@0@'.format(meson.current_source_dir() / 'libpmem2.link.in')
elif host_machine.system() == 'windows'
    link_args += '-DEF:@0@'.format(meson.current_source_dir() / 'libpmem2.def')
    link_args += ['shlwapi.lib', 'ntdll.lib', 'mincore.lib', 'kernel32.lib', 'user32.lib', 'gdi32.lib', 'winspool.lib', 'comdlg32.lib', 'advapi32.lib', 'shell32.lib', 'ole32.lib', 'oleaut32.lib', 'uuid.lib', 'odbc32.lib', 'odbccp32.lib']
endif

libpmem2 = library(
    'pmem2',
    sources,
    c_args: [
        '-DPMEM2_USE_MINIASYNC',
    ],
    link_args: link_args,
    dependencies: [
        libndctl_dep,
        libdaxctl_dep,
        pmemcore_dep,
        pmem2arch_dep,
        threads_dep,
    ],
    include_directories: [
        root_include,
        common_include,
        miniasync_include,
    ],
    name_prefix: 'lib',
    install: true
)

libpmem2_dep = declare_dependency(
    link_with: libpmem2,
    include_directories: root_include,
)

libpmem2_internal_dep = declare_dependency(
    sources: sources,
    compile_args: [
        '-DPMEM2_USE_MINIASYNC',
    ],
    dependencies: [
        libndctl_dep,
        libdaxctl_dep,
        pmemcore_dep,
        pmem2arch_dep,
        threads_dep,
    ],
    include_directories: [
        include_directories('.'),
        root_include,
        common_include,
        miniasync_include,
    ],
)

if meson.version().version_compare('>=0.54')
    meson.override_dependency('libpmem2', libpmem2_dep)
endif
